{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Control Óptimo de Cohete \n",
    "\n",
    "<img src=\"YouWillNotGoToSpaceToday.png\" width=\"350\">\n",
    "\n",
    "(Credito de Imagen a www.XKCD.com)\n",
    "\n",
    "Se estudia el problema de Goddard, formulado por Robert H. Goddard en 1919 en \"_A Method of Reaching Extreme Altitudes_\". \n",
    "\n",
    "Consiste en definir el control óptimo de la potencia aplicada del motor de manera de lograr la máxima altura en el vuelo del cohete, tomando en cuenta el arrastre aerodinámico, el cambio en la masa del cohete y la variación en la fuerza de gravedad.\n",
    "\n",
    "## Dinámica\n",
    "\n",
    "La segunda ley de newton requiere que:\n",
    "\n",
    "$$\\frac{d}{dt}(m v) = \\sum F_{ext}$$\n",
    "\n",
    "Asumimos que el movimiento del cohete es solamente en la direccion radial al centro de la tierra (...hacia afuera...), la coordenada $h(t)$ mide la posición del cohete desde el centro de la tierra.\n",
    "\n",
    "La velocidad es igual a $\\dot{h}$, la masa del cohete es naturalmente variable $m(t)$ y las fuerzas aplicadas al cohete son la gravedad ($g(h)$) y el arrastre aerodinámico ($D(h,\\dot{h})$).\n",
    "\n",
    "Usando las hipótesis anteriores, la segunda ley de newton nos da:\n",
    "\n",
    "$$ m \\ddot{h} = -\\dot{m} v_e -g(h)-D(h,\\dot{h}) $$\n",
    "\n",
    "El primer término se puede interpretar como el empuje producido por el motor, la pérdida de masa ($\\dot{m}<0$) se corresponde con una fuerza aplicada en el sentido de la velocidad del cohete. Llamaremos a esa fuerza el Empuje (Thrust), la notaremos como $T$ y será nuestra variable de control.\n",
    "\n",
    "Tenemos por lo tanto el sistema de ecuaciones:\n",
    "\n",
    "$$\\left\\{ \\begin{eqnarray}\n",
    "m \\ddot{h} &=& T -g(h)-D(h,\\dot{h}) \\\\\n",
    "\\dot{m} &=& -\\frac{T}{v_e}\n",
    "\\end{eqnarray}\\right.\n",
    "$$\n",
    "\n",
    "La ley de gravedad es sencillamente:  $g(h) = g_0 (h_0/h)^2$\n",
    "\n",
    "La definición del arrastre aerodinámico es: $D(h,\\dot{h})=1/2 C_d A_d \\left(\\rho_0 e^{k(1-h/h_0)}\\right) \\dot{h}^2 $. Se toma en cuenta aquí una atmósfera que se atenúa de manera exponencial.\n",
    "\n",
    "El sistema dinámico se puede adimensionar de manera de trabajar sin unidades. Por ejemplo: $\\hat{h}=h/R$, siendo $R$ el radio de la tierra. Otro ejemplo: $\\hat{m}=m/m_0$, siendo $m_0$ la masa inicial del cohete. La referencia optó por expresar el problema en forma adimensionada. Llevar los resultados a expresiones con dimensión requiere simplemente multiplicar los resultados por las constantes correctas.\n",
    "\n",
    "\n",
    "## Comentario de Solución Mediante Principio del Mínimo de Poyntragin\n",
    "\n",
    "Se puede ver claramente que el Hamiltoniano va a ser lineal en la variable de control $T$. Por lo tanto el principio del Mínimo lleva a estudiar las situaciones en las cuales el coeficiente que multiplica a $T$ es positivo o negativo (para obtener control tipo Bang-Bang) y también cuando es cero.\n",
    "\n",
    "Cuando el factor que multiplica a $T$ es cero, la condicion de mínimo no depende de $T$ y debemos recurrir a las ecuaciones adjuntas para hallar cuál es el comportamiento de $T$ en esa circunstancia. Este tipo de soluciones de control se llaman **Arcos Singulares**.\n",
    "\n",
    "En la solucion numérica mostramos un control óptimo de tipo _Bang-Arco Singular_ e inclusive Bang-Arco Singular-Bang_.\n",
    "\n",
    "## Solución Numérica del Control Óptimo\n",
    "Referencia a versión original del notebook: https://www.juliaopt.org/notebooks/JuMP-Rocket.html"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Ipopt"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create JuMP model, using Ipopt as the solver\n",
    "mod = Model(with_optimizer(Ipopt.Optimizer,print_level=0))\n",
    "\n",
    "# Constants\n",
    "# Note that all parameters in the model have been normalized\n",
    "# to be dimensionless. See the COPS3 paper for more info.\n",
    "h_0 = 1    # Initial height\n",
    "v_0 = 0    # Initial velocity\n",
    "m_0 = 1    # Initial mass\n",
    "g_0 = 1    # Gravity at the surface\n",
    "\n",
    "# Parameters\n",
    "T_c = 3.5  # Used for thrust\n",
    "h_c = 500  # Used for drag \n",
    "v_c = 620  # Used for drag **VER 3 CASOS: 200,620,1000.\n",
    "m_c = 0.6  # Fraction of initial mass left at end ** Ver 0.6 y CASO: 0.4\n",
    "\n",
    "# Derived parameters\n",
    "c     = 0.5*sqrt(g_0*h_0)  # Thrust-to-fuel mass\n",
    "m_f   = m_c*m_0            # Final mass\n",
    "D_c   = 0.5*v_c*m_0/g_0    # Drag scaling\n",
    "T_max = T_c*g_0*m_0        # Maximum thrust\n",
    "\n",
    "n = 1000                                    # Time steps\n",
    "@variable(mod, Δt ≥ 0, start = 1/n)        # Time step\n",
    "@NLexpression(mod,t_f, Δt*n)               # Time of flight\n",
    "\n",
    "# State variables\n",
    "@variable(mod, v[0:n] ≥ 0)            # Velocity\n",
    "@variable(mod, h[0:n] ≥ h_0)          # Height\n",
    "@variable(mod, m_f ≤ m[0:n] ≤ m_0)    # Mass\n",
    "\n",
    "# Control: thrust\n",
    "@variable(mod, 0 ≤ T[0:n] ≤ T_max)\n",
    "\n",
    "# Objective: maximize altitude at end of time of flight\n",
    "@objective(mod, Max, h[n])\n",
    "\n",
    "# Initial conditions\n",
    "@constraint(mod, v[0] == v_0)\n",
    "@constraint(mod, h[0] == h_0)\n",
    "@constraint(mod, m[0] == m_0)\n",
    "\n",
    "# Forces\n",
    "# Drag(h,v) = Dc v^2 exp( -hc * (h - h0) / h0 )\n",
    "@NLexpression(mod,drag[j=0:n], D_c*(v[j]^2)*exp(-h_c*(h[j]-h_0)/h_0))\n",
    "# Grav(h)   = go * (h0 / h)^2\n",
    "@NLexpression(mod,grav[j=0:n], g_0*(h_0/h[j])^2)\n",
    "\n",
    "# Dynamics\n",
    "for j in 1:n\n",
    "    # h' = v\n",
    "    # Rectangular integration\n",
    "    # @NLconstraint(mod, h[j] == h[j-1] + Δt*v[j-1])\n",
    "    # Trapezoidal integration\n",
    "    @NLconstraint(mod,\n",
    "        h[j] == h[j-1] + 0.5*Δt*(v[j]+v[j-1]))\n",
    "\n",
    "    # v' = (T-D(h,v))/m - g(h)\n",
    "    # Rectangular integration\n",
    "    # @NLconstraint(mod, v[j] == v[j-1] + Δt*(\n",
    "    #                 (T[j-1] - drag[j-1])/m[j-1] - grav[j-1]))\n",
    "    # Trapezoidal integration\n",
    "    @NLconstraint(mod,\n",
    "        v[j] == v[j-1] + 0.5*Δt*(\n",
    "            (T[j  ] - drag[j  ] - m[j  ]*grav[j  ])/m[j  ] +\n",
    "            (T[j-1] - drag[j-1] - m[j-1]*grav[j-1])/m[j-1] ))\n",
    "\n",
    "    # m' = -T/c\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, m[j] == m[j-1] - Δt*T[j-1]/c)\n",
    "    # Trapezoidal integration\n",
    "    @NLconstraint(mod,\n",
    "        m[j] == m[j-1] - 0.5*Δt*(T[j] + T[j-1])/c)\n",
    "end\n",
    "\n",
    "# Provide starting solution\n",
    "for k in 0:n\n",
    "    set_start_value(h[k], 1)\n",
    "    set_start_value(v[k], (k/n)*(1 - (k/n)))\n",
    "    set_start_value(m[k], (m_f - m_0)*(k/n) + m_0)\n",
    "    set_start_value(T[k], T_max/2)\n",
    "end\n",
    "\n",
    "# Print the optimization problem (mod)\n",
    "#print(mod)\n",
    "println(\"\")\n",
    "\n",
    "# Solve for the control and state\n",
    "println(\"Solving...\")\n",
    "status = optimize!(mod)\n",
    "\n",
    "# Display results\n",
    "println(\"Solver status: \", termination_status(mod))\n",
    "println(\"Max height: \", objective_value(mod))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Presentación de los resultados numéricos"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Gadfly"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Determino las constantes para volver a tener dimensiones de [km] [seg]:\n",
    "\n",
    "R_t = 6371 # radio de la tierra [km]\n",
    "G_0 = 9.81 # aceleracion gravitatoria a nivel del mar [m/s^2]\n",
    "c_t = sqrt(R_t*1000/G_0) # constante adimensionado para el tiempo [seg]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "tiempo = collect((0:n)*value(Δt)*c_t)\n",
    "\n",
    "h_plot = plot(x=tiempo,y=collect(value.(h*R_t)), Geom.line,\n",
    "                Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Altitude [km]\"))\n",
    "m_plot = plot(x=tiempo,y=collect(value.(m)), Geom.line,\n",
    "                Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Mass\"))\n",
    "v_plot = plot(x=tiempo,y=collect(value.(v)*R_t/c_t), Geom.line,\n",
    "                Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Velocity (km/s)\"))\n",
    "T_plot = plot(x=tiempo,y=collect(value.(T)), Geom.line,\n",
    "                Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Thrust\"))\n",
    "d_plot = plot(x=tiempo,y=collect(value.(drag)), Geom.line,\n",
    "                Guide.xlabel(\"Time (s)\"), Guide.ylabel(\"Drag\"))\n",
    "f_plot = plot(x=collect(value.(h)*R_t),y=collect(value.(v*R_t/c_t)), Geom.line,\n",
    "                Guide.xlabel(\"Altitude [km]\"), Guide.ylabel(\"Velocity [km/s]\"))\n",
    "draw(SVG(6inch, 9inch), vstack( hstack(h_plot,m_plot),\n",
    "                                hstack(v_plot,T_plot),\n",
    "                                hstack(d_plot,f_plot)))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.2.0",
   "language": "julia",
   "name": "julia-1.2"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.2.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
